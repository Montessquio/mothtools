Crucible:
    <GlobalAttr>* <Unit>*

Unit:
    <LocalAttr>* <Namespace>
  | <LocalAttr>* <Component>

Namespace:
    "namespace" <DefKey> "{" <Unit>* "}"

InheritRef:
    "from" <DefKey>

Component:
    <InheritRef>? <ObjDef> 
    
ObjDef:
    <Aspect>
  | <Card>  
  | <Deck>  
  | <Recipe>
  | <Verb>  
  | <Legacy>
  | <Ending>

Aspect:
    <AspectDef>
  | <AspectQuick>

AspectDef:
    <Hidden>? "aspect" <DefKey> "{" <AspectStatement>* "}"

AspectQuick:
    <Hidden>? "aspect" <DefKey> STRING_LITERAL <DescriptionParam>? "->" <Chance> <DefKey> "{" <AspectStatement> "}" "->" <DefKey> <Chance>?

AspectStatement:
    "SET" "=" <Value>
  | "DECAY" <DefKey>
  | "SPAWN" <DefKey> <Chance>?
  | <Xtrigger>

Card:
    <CardDef>
  | <CardQuick>

CardDef:
    <Hidden>? "card" <DefKey> "{" <CardStatement> "}"

CardQuick:
    <Hidden>? "card" <DefKey> STRING_LITERAL <DescriptionParam>? <ParamList>? "->" NUMBER <DefKey> "{" <CardQuickStatement>* "}" "->" <DefKey> <Chance>?

CardStatement:
    "SET" "=" <Value>
  | "ASPECTS" <CardRequirements>
  | "UNIQUE" <DefKey>?
  | "DECAY" NUMBER <DefKey>?
  | "SPAWN" <DefKey> <Chance>?
  | <DefKey> "->" <SlotDef>
  | <Xtrigger>

CardStatementQuick:
    "SET" "=" <Value>
  | "UNIQUE" <DefKey>?
  | <DefKey> "->" <SlotDef>
  | <Xtrigger>

Deck:
    deck" <DefKey> STRING_LITERAL <DescriptionParam>? "{" <DeckCardRef>* "}"

DeckCardRef:
    "!" <DefKey>
  | <DefKey> STRING_LITERAL
  | <DefKey>

Recipe:
    <RecipeKind>? <RecipeKind>? "recipe" <DefKey> <DefKey> "(" <RecipeRequirement>+ ")" <MaxExecutions>? <RecipeStage>+

RecipeKind:
    "craft" "hint"
  | "hint" "craft"
  | "craft"
  | "hint"
  | "!" "?"
  | "?" "!"
  | "?"
  | "!"

RecipeRequirement:
    "TABLE" <QuantityDefKey>
  | "EXTANT" <QuantityDefKey>
  | <QuantityDefKey>

RecipeStage:
    "{" <RecipeStatement>+ "}"

RecipeStatement:
    "SET" "=" <Value>
  | "WARMUP" NUMBER
  | "APPLY" <ApplyParams>
  | "DRAW" <DefKey> NUMBER?
  | "SIGNAL" <DefKey>
  | "PURGE" <DefKey> NUMBER?
  | "BURN" <DefKey>
  | "PORTAL" <DefKey>
  | "ENDING" <DefKey> <DefKey>?
  | "HALT" <DefKey>?
  | "DELETE" <DefKey>?
  | <SlotDef>
  | <Branch>

Branch:
    "LINK" <DefKey> <BranchCondition>?
  | "GOTO" <DefKey> <BranchCondition>? <SpawningKind>?

BranchCondition:
    "if" <Chance> "(" <RecipeRequirement>* ")"
  | "if" "(" <RecipeRequirement>* ")"
  | <Chance>

SpawningKind:
    "->" "SPAWN"
  | "->" "EXPEL" ExpelDefs?

ExpelDefs:
    "{" <QuantityDefKey>* "}"

ApplyParams:
    <DefKey>? <DefKey> <ApplyOp> NUMBER
  | <DefKey> "=" <DefKey>

Verb:
    "verb" <DefKey> STRING_LITERAL <DescriptionParam> <VerbSlot>?

VerbSlot:
    "(" <SlotDef>? ")"

SlotDef:
    <SlotKind>? <DefKey> STRING_LITERAL <DescriptionParam> <SlotParams>?

SlotKind:
    "consume" "greedy"
  | "greedy" "consume"
  | "consume"
  | "greedy"
  | "!" "?"
  | "?" "!"
  | "?"
  | "!"

SlotParams:
    "(" <SlotFilter>* ")"

SlotFilter:
    "!"? <QuantityDefKey>

ParamList:
    "(" <QuantityDefKey>+ ")"

QuantityDefKey:
    <DefKey> ":" NUMBER
  | <DefKey>

MaxExecutions:
    "max" NUMBER

DescriptionParam:
    "[" STRING_LITERAL "]"

Xtrigger:
    "XTRIGGER" <DefKey> "->" <XtriggerBody>
    
XtriggerBody:
    <DefKey> <Chance>?
  | "SPAWN" <QuantityDefKey> <Chance>?
  | "MUTATE" <QuantityDefKey> <Chance>?

Hidden:
    "hidden"
  | "?"

Temp:
    "temp"
  | "?"
  
Chance:
    NUMBER "%"

Value:
    STRING_LITERAL
  | NUMBER
  | BOOLEAN

BOOLEAN:
    "true"
  | "false"

GlobalAttr:
    '#' '!' '[' STRING_LITERAL ']'

LocalAttr:
    '#' '[' STRING_LITERAL ']'